plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'guru.qa'
version '1.0-SNAPSHOT'

allure {
    report {
        version.set("2.20.1")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.20.1") //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}


compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.9.1',
            'com.codeborne:selenide:6.10.1',
            'io.qameta.allure:allure-selenide:2.20.1',
            'com.github.javafaker:javafaker:1.0.2',
            'org.slf4j:slf4j-simple:1.7.32'
    )
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task smoke_tests(type: Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }
}